load_file("OLM.hoc")

//simuation constants
SIMRUNTIME = 600
DELAY = 200
INJTIME = 250
I_INJ = 0.8

tstop = SIMRUNTIME
v_init = -68

objectvar membrane_voltage, time
membrane_voltage = new Vector()
time = new Vector()

objectvar cell
cell = new OLMCell()

//record the cells membrane voltage.
membrane_voltage.record(&cell.soma.v(0.5))
time.record(&t)

//First graph the membrane potential vs time plot.

//Create a current clamp.
objectvar i_inj
cell.soma i_inj = new IClamp(0.5)
i_inj.del = DELAY
i_inj.dur = INJTIME
i_inj.amp = I_INJ

//run the simulation and plot the outcome
run()

//Membrane voltage vs. time
objectvar g_mvt
g_mvt = new Graph()
g_mvt.size(0,SIMRUNTIME, 30, -90)
g_mvt.vector(membrane_voltage.size(), &time.x[0], &membrane_voltage.x[0])
g_mvt.flush()


//Record the currents.
objref g_cvt, kap_vec, nat_vec, kdr_vec, nap_vec, im_vec
g_cvt = new Graph()
nat_vec = new Vector()
kdr_vec = new Vector()
nap_vec = new Vector()
im_vec = new Vector()


nat_vec.record(&cell.soma[0].i_natOLM(0.5))
kdr_vec.record(&cell.soma[0].i_kdrOLM(0.5))
nap_vec.record(&cell.soma.i_napOLM(0.5))
im_vec.record(&cell.soma.i_imOLM(0.5))

objref nc, nil, spvec
func runSim(){


    cell.soma nc = new NetCon(&v(0.5),nil)
    spvec = new Vector()
    nc.record(spvec)

    i_inj.amp=$1

    run()

    return (spvec.size() / (INJTIME)) * 1000
}

printf("Cell fi at %f nA injection: %f\n",I_INJ,runSim(I_INJ))

proc cgraph() {
    //Graph currents.
    g_cvt.color(2)
    g_cvt.label("Red: nat")
    g_cvt.vector(nat_vec.size(), &time.x[0], &nat_vec.x[0])

    g_cvt.color(4)
    g_cvt.label("Green: kdr")
    g_cvt.vector(kdr_vec.size(), &time.x[0], &kdr_vec.x[0])

    g_cvt.color(7)
    g_cvt.label("Violet: im")
    g_cvt.vector(im_vec.size(), &time.x[0], &im_vec.x[0])

    g_cvt.color(5)
    g_cvt.label("Orange: nap")
    g_cvt.vector(nap_vec.size(), &time.x[0], &nap_vec.x[0])
    
    
    g_cvt.flush()

    g_cvt.exec_menu("View = plot")
}

cgraph()


objectvar g_fi, target, reported, current_inj_reported, current_inj_target
g_fi = new Graph()
reported = new Vector(10)
current_inj_reported = new Vector(10)

count = 0
for (i = 0; i < 1.0; i += 0.1) {
    reported.x[count] = runSim(i)
    current_inj_reported.x[count] = i
    count += 1
}

current_inj_target = new Vector(2)
current_inj_target.x[0] = 0.4
current_inj_target.x[1] = 0.8

target = new Vector(2)
target.x[0] = 48
target.x[1] = 92

g_fi.color(4)
g_fi.vector(2, &current_inj_target.x[0],&target.x[0])


g_fi.color(1)
g_fi.vector(10, &current_inj_reported.x[0],&reported.x[0])

g_fi.size(0,1.0,0,target.x[target.size() - 1] + 10)

g_fi.flush()
